plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId 'demo.retoast'
        versionName getGitCommitShortId()
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }
    lintOptions {
        textReport true

        /* It is only for the convenience of checking Toasts' behavior with or without ReToast. */

        disable 'ReToastImportWay'
        disable 'ReToastInitializerCalling'
    }
}

dependencies {
    implementation project(':retoast')
}

def getGitCommitShortId() {
    try {
        return 'git rev-parse --short HEAD'.execute().text.trim()
    } catch (IOException e) {
        logger.lifecycle("Git not installed", e)
        return "unkn0wn"
    }
}
