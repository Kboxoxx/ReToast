// gradle task: publish
// (Dir: local-mvn-repo)

apply plugin: 'maven-publish'

group = PROJ_GROUP
version = PROJ_VERSION
project.archivesBaseName = PROJ_ARTIFACT_ID

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = PROJ_GROUP
                artifactId = PROJ_ARTIFACT_ID
                version = PROJ_VERSION

                pom {
                    description = PROJ_DESCRIPTION
                    name = PROJ_NAME
                    url = PROJ_WEBSITE_URL

                    licenses {
                        license {
                            name = LICENSE_NAME
                            url = LICENSE_URL
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = DEVELOPER_ID
                            name = DEVELOPER_NAME
                            email = DEVELOPER_EMAIL
                        }
                    }
                    scm {
                        connection = PROJ_VCS_URL
                        url = PROJ_WEBSITE_URL
                    }
                }
            }
        }
        repositories {
            maven { url './../local-mvn-repo' }

            if (System.getenv("PUBLISH_GP") == true.toString()) {
                maven {
                    name 'GitHubPackages'
                    url = GITHUB_PKG_PUBLISH_URL
                    credentials {
                        username System.getenv('GITHUB_ACTOR')
                        password System.getenv('GITHUB_TOKEN')
                    }
                }
            }

        }
    }
}
